module org.franca.compmodel.dsl.GenerateFComp

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.franca.compmodel.dsl"
				rootPath = rootPath
				runtimeTest = {
					root =  "../../tests/org.franca.compmodel.dsl.tests"
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = false
				}
				genericIde = {
					enabled = true
		        }
				createEclipseMetaData = true	
			}
			code = {
				encoding = "ISO-8859-1"
				lineDelimiter = "\r\n"
				fileHeader = "/* Copyright (C) 2018 BMW Group
 * Author: Bernhard Hennlich (bernhard.hennlich@bmw.de)
 * This Source Code Form is subject to the terms of the Eclipse Public
 * License, v. 1.0. If a copy of the EPL was not distributed with this
 * file, You can obtain one at https://www.eclipse.org/legal/epl-v10.html.
 */"
			}
		}
		language = StandardLanguage {
			referencedResource = "platform:/resource/org.franca.core/model/franca.genmodel"
			name = "org.franca.compmodel.dsl.FComp"
			fileExtensions = "fcdl"
			
			serializer = {
				generateStub = false
			}

			formatter = {
				generateStub = true
			}
			
			validator = {
				composedCheck = "org.eclipse.xtext.validation.ImportUriValidator"
				//composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
		}
	}
}